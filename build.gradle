plugins {
	id 'maven-publish'
	id 'com.github.hierynomus.license' version '0.16.1' apply false
	id 'dev.architectury.loom' version '1.10-SNAPSHOT' apply false

	// https://github.com/ReplayMod/preprocessor
	// https://github.com/Fallen-Breath/preprocessor
	id 'com.replaymod.preprocess' version '0a04a5668b'

	// https://github.com/Fallen-Breath/yamlang
	id 'me.fallenbreath.yamlang' version '1.4.1' apply false
}

preprocess {
    strictExtraMappings = false

	def mc115_fabric  = createNode('1.15.2-fabric', 1_15_02, '')
	def mc116_fabric  = createNode('1.16.5-fabric', 1_16_05, '')
	def mc117_fabric  = createNode('1.17.1-fabric', 1_17_01, '')
	def mc1206_fabric = createNode('1.20.6-fabric', 1_20_06, '')

	def mc115_forge  = createNode('1.15.2-forge', 1_15_02, '')
	def mc116_forge  = createNode('1.16.5-forge', 1_16_05, '')
	def mc117_forge  = createNode('1.17.1-forge', 1_17_01, '')
	def mc1206_forge = createNode('1.20.6-forge', 1_20_06, '')

	def mc1206_neoforge = createNode('1.20.6-neoforge', 1_20_06, '')

	mc115_fabric .link(mc116_fabric , null)
	mc116_fabric .link(mc117_fabric , null)
	mc117_fabric .link(mc1206_fabric , null)

	mc115_fabric .link(mc115_forge , null)
	mc116_fabric .link(mc116_forge , null)
	mc117_fabric .link(mc117_forge , null)
	mc1206_fabric.link(mc1206_forge, null)

	mc1206_fabric.link(mc1206_neoforge, null)
}

tasks.register('buildAndGather') {
	subprojects {
		dependsOn project.tasks.named('build').get()
	}
	doFirst {
		println 'Gathering builds'
		def buildLibs = {
			p -> p.buildDir.toPath().resolve('libs')
		}
		delete fileTree(buildLibs(rootProject)) {
			include '*'
		}
		subprojects {
			copy {
				from(buildLibs(project)) {
					include '*.jar'
					exclude '*-dev.jar', '*-sources.jar', '*-shadow.jar'
				}
				into buildLibs(rootProject)
				duplicatesStrategy = DuplicatesStrategy.INCLUDE
			}
		}
	}
}
